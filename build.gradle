buildscript {
  repositories {
    maven { url "https://repo.spring.io/plugins-release" }
  }
  dependencies {
    classpath "io.spring.gradle:propdeps-plugin:0.0.10.RELEASE"
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
  id "org.jetbrains.kotlin.jvm" version "1.3.72" apply false
  id "org.jetbrains.kotlin.plugin.spring" version "1.3.72"
  id 'org.jetbrains.dokka' version '0.10.1' apply false
}

ext {
  kotlinVersion = "1.3.72"
  springBootVersion = "2.3.2.RELEASE"
  slf4jVersion = "1.7.30"
  logbackVersion = "1.2.3"

  gradleScriptDir = "${rootProject.projectDir}/gradle"

  withoutJclOverSlf4j = {
    exclude group: "org.slf4j", name: "jcl-over-slf4j"
  }
  withoutSlf4JApi = {
    exclude group: "org.slf4j", module: "slf4j-api"
  }
}

configure(allprojects) { project ->
  group = "com.openjdl"
  sourceCompatibility = '1.8'

  apply plugin: "java"
  apply plugin: "kotlin"
  apply plugin: "kotlin-spring"
  apply plugin: "propdeps"
  apply plugin: "io.spring.dependency-management"
  apply from: "${gradleScriptDir}/ide.gradle"
  apply plugin: "maven"
  apply plugin: "maven-publish"

  pluginManager.withPlugin("kotlin") {
    apply plugin: "org.jetbrains.dokka"
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        languageVersion = "1.3"
        apiVersion = "1.3"
        freeCompilerArgs = ["-Xjsr305=strict"]
        allWarningsAsErrors = true
      }
    }
    compileTestKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xjsr305=strict"]
      }
    }
  }

  configurations.all {
    // Check for updates every build
    resolutionStrategy {
      cacheChangingModulesFor 0, "seconds"
      cacheDynamicVersionsFor 0, "seconds"
    }

    // Consistent slf4j version (e.g. clashes between slf4j versions)
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
      if (details.requested.group == "org.slf4j") {
        details.useVersion slf4jVersion
      }
    }
  }

  compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.encoding = "UTF-8"
  }

  compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    options.encoding = "UTF-8"
    options.compilerArgs += "-parameters"
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      freeCompilerArgs = ["-Xjsr305=strict"]
    }
  }

  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "1.8"
      freeCompilerArgs = ["-Xjsr305=strict"]
    }
  }

  repositories {
    maven { url "https://maven.aliyun.com/repository/public/" }
    maven { url "https://maven.aliyun.com/repository/spring/" }
    maven { url "https://jitpack.io" }
    mavenLocal()
    mavenCentral()
  }

  test {
    useJUnitPlatform()
    include(["**/*Tests.class", "**/*Test.class"])
  }

  dependencies {
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}") {
      exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
  }
}

configure(subprojects) { subproject ->
  apply from: "${gradleScriptDir}/publish-maven.gradle"

  jar {
    manifest.attributes["Implementation-Title"] = subproject.name
    manifest.attributes["Implementation-Version"] = subproject.version
    manifest.attributes["Automatic-Module-Name"] = subproject.name.replace('-', '.')  // for Jigsaw
    manifest.attributes["Created-By"] =
            "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    classifier = "sources"
    from sourceSets.main.allSource
    // Don't include or exclude anything explicitly by default. See SPR-12085.
  }

  artifacts {
    archives sourcesJar
  }

  publishing {
    publications {
      openjdl(MavenPublication) {
        from components.java
        artifact sourcesJar
      }
    }

    repositories {
      maven {
        name = 'openjdl'
        // 可以配置在~/.gradle/gradle.properties
        // NEXUS_SNAPSHOT_REPO_URL=http://xxx/repository/maven-snapshots/
        // NEXUS_RELEASE_REPO_URL=http://xxx/repository/maven-releases/
        //NEXUS_PASSWORD=xxx
        //NEXUS_USERNAME=xxx
        url = version.endsWith('SNAPSHOT') ? NEXUS_SNAPSHOT_REPO_URL : NEXUS_RELEASE_REPO_URL
        credentials {
          username NEXUS_USERNAME
          password NEXUS_PASSWORD
        }
      }
    }
  }
}

configure(rootProject) {
  description = "OpenJDL's Java Server Foundation"

//  apply plugin: "groovy"

  // Don't publish the default jar for the root project
  configurations.archives.artifacts.clear()

}
